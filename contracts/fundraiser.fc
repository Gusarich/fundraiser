#include "imports/stdlib.fc";
#include "constants.fc";

global slice data::admin;
global int data::goal;
global cell data::current;
global int data::block_time;
global slice data::priority_coin;
global cell data::metadata_ipfs_link;
global slice data::fee_receiver;
global int data::fee_percentage;

global int context::op;
global int context::query_id;
global slice context::sender;

() load_data() impure {
    slice ds = get_data().begin_parse();
    data::admin = ds~load_msg_addr();
    data::goal = ds~load_coins();
    data::current = ds~load_dict();
    data::block_time = ds~load_uint(64);
    data::priority_coin = ds~load_msg_addr();
    data::metadata_ipfs_link = ds~load_ref();
    data::fee_receiver = ds~load_msg_addr();
    data::fee_percentage = ds~load_uint(16);
}

() save_data() impure {
    set_data(begin_cell()
        .store_slice(data::admin)
        .store_coins(data::goal)
        .store_dict(data::current)
        .store_uint(data::block_time, 64)
        .store_slice(data::priority_coin)
        .store_ref(data::metadata_ipfs_link)
        .store_slice(data::fee_receiver)
        .store_uint(data::fee_percentage, 16)
    .end_cell());
}

(slice, int) dict_get?(cell dict, int key_len, slice index) asm(index dict key_len) "DICTGET" "NULLSWAPIFNOT";

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_bits() < 96) {
        return ();
    }

    context::op = in_msg_body~load_uint(32);
    context::query_id = in_msg_body~load_uint(64);
    (_, context::sender) = in_msg_full.begin_parse().load_msg_addr();

    load_data();

    if (context::op == op::claim) {
        throw_unless(error::wrong_sender, equal_slices(context::sender, data::admin));
        (slice key, slice val, int flag) = data::current~dict::delete_get_min(267);
        while (flag) {
            int amount = val~load_coins();
            int fee_amount = amount * data::fee_percentage / 10000;
            
            send_raw_message(begin_cell()
                .store_uint(0x18, 6)
                .store_slice(key)
                .store_coins(const::jetton_transfer_fee)
                .store_uint(0, 107)
                .store_uint(op::transfer, 32)
                .store_uint(context::query_id, 64)
                .store_coins(amount - fee_amount)
                .store_slice(data::admin)
                .store_uint(0, 6)
            .end_cell(), 1);

            send_raw_message(begin_cell()
                .store_uint(0x18, 6)
                .store_slice(key)
                .store_coins(const::jetton_transfer_fee)
                .store_uint(0, 107)
                .store_uint(op::transfer, 32)
                .store_uint(context::query_id, 64)
                .store_coins(fee_amount)
                .store_slice(data::fee_receiver)
                .store_uint(0, 6)
            .end_cell(), 1);

            (key, val, flag) = data::current~dict::delete_get_min(267);
        }
    }

    elseif (context::op == op::transfer_notification) {
        int amount = in_msg_body~load_coins();

        (slice current_amount_slice, int f?) = data::current.dict_get?(267, context::sender);
        int current_amount = amount;
        if (f?) {
            current_amount += current_amount_slice~load_coins();
        }
        
        data::current~dict_set(267, context::sender, begin_cell().store_coins(current_amount).end_cell().begin_parse());
    }

    else {
        throw(0xffff);
    }

    save_data();
}
